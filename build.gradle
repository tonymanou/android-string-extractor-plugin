plugins {
  id "com.jfrog.bintray" version "1.7.2"
  id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'de.ito.gradle.plugin'
version = '0'

repositories {
  google()
  jcenter()
}

configurations {
  compileInto
}

dependencies {
  compileOnly 'com.android.tools.build:gradle:3.1.4'
  compileInto 'org.apache.commons:commons-lang3:3.4'
  compileInto 'org.apache.poi:poi-ooxml:3.15'
  compileInto 'org.marketcetera.fork:commons-csv:2.4.2'
  testCompile 'org.mockito:mockito-core:1.+'
  testCompile 'junit:junit:4.12'
  configurations.compile.extendsFrom(configurations.compileInto)
}

gradlePlugin {
  plugins {
    androidStringExtractor {
      id = 'android-string-extractor'
      implementationClass =
              'de.ito.gradle.plugin.androidstringextractor.AndroidStringExtractorPlugin'
    }
  }
}

jacoco {
  toolVersion '0.7.7.201606060606'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

jar {
  from {
    configurations.compileInto.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'android-string-extractor'
      version project.version
    }
  }
}

bintray {
  user = project.hasProperty('bintray_user') ? project.bintray_user : ''
  key = project.hasProperty('bintray_key') ? project.bintray_key : ''

  publications = ['mavenJava']
  dryRun  = false
  publish = false
  pkg {
    repo = 'maven'
    name = 'de.ito.gradle.plugin:android-string-extractor'
    desc = 'Gradle plugin which automatically extracts hardcoded strings from Android layouts.'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/it-objects/android-string-extractor-plugin.git'
    labels = ['Android', 'Gradle', 'plugin', 'string', 'extraction']
    publicDownloadNumbers = false
    version {
      name = project.version
      desc = ''
    }
  }
}
